module AgdaMode.Extension.Model where

open import Agda.Builtin.Unit

open import Data.Maybe
open import Data.Maybe.Effectful
open import Data.String hiding (show)
import Data.String as String
open import Data.Map
open import Data.List hiding (_++_ ; head)
open import Data.Product
open import Data.Nat renaming (show to show-Nat)
import Data.Nat as Nat
open import Data.Bool
open import Function hiding (id)
open import Data.IO
open import Data.JSON.Decode
open import Effect.Monad

open Monad {{ ... }}
open MonadPlus {{ ... }} using (_<|>_ ; ⊘)

open import AgdaMode.Extension.Highlighting
open import AgdaMode.Extension.Highlighting.Decode
open import AgdaMode.Extension.Keymap
open import AgdaMode.Extension.Goals
open import AgdaMode.Extension.Position

open import Vscode.Panel
open import Vscode.Window
open import Vscode.Common
open import Vscode.SemanticTokensProvider
open import Vscode.TextEditor

open import Node.Process

module InteractionPoint where
  record t : Set where
    constructor mkInteractionPoint
    field
      id : Nat
      range : OffsetRange.t
  open t public

  content-range : t → OffsetRange.t
  content-range (mkInteractionPoint _ range) = offset-range (range .start + 2) (range .length - 4)

  show : t → String
  show (mkInteractionPoint id range) =
    "mkInteractionPoint " ++ Nat.show id ++ " " ++ "(" ++ OffsetRange.show range ++ ")"

  equals? : t → t → Bool
  equals? a b = OffsetRange.equals? (a .range) (b .range) ∧ a .id Nat.== b .id

  private
    ip-range-decoder : Decoder OffsetRange.t
    ip-range-decoder = do
      start ← required "start" (required "pos" nat |> fmap (_- 1))
      end ← required "end" (required "pos" nat |> fmap (_- 1))
      pure $ offset-range start (end - start)

  decoder : Decoder t
  decoder = (| mkInteractionPoint (required "id" nat) (list ip-range-decoder |> index 0 |> required "range") |)

open InteractionPoint using (mkInteractionPoint ; id ; range) public

record File : Set where
  constructor mkFile
  field
    interaction-points : List InteractionPoint.t
    tokens : List Token.t
open File public

record Model : Set where field
  panel : Maybe (Panel.t ⊤)
  status-bar-item : StatusBarItem.t
  input-mode-status-item : StatusBarItem.t
  agda : Maybe Process.t
  stdout-buffer : String
  current-doc : Maybe TextDocument.t
  loaded-files : StringMap.t File
  tokens-request-emitter : EventEmitter.t ⊤
  running-info : String
  underline-decoration ip-decoration : DecorationType.t
  keymap : Trie.t
open Model public

module Rewrite where
  data t : Set where
    as-is instantiated head-normal simplified normalised : t

  show : t → String
  show as-is = "AsIs"
  show instantiated = "Instantiated"
  show head-normal = "HeadNormal"
  show simplified = "Simplified"
  show normalised = "Normalised"

  decoder : Decoder t
  decoder = string >>= λ where
    "AsIs" → succeed as-is
    "Instantiated" → succeed instantiated
    "HeadNormal" → succeed head-normal
    "Simplified" → succeed simplified
    "Normalised" → succeed normalised
    _ → ⊘
open Rewrite hiding (t ; show ; decoder) public

module Backend where
  data t : Set where
    ghc : Bool → t
    js html latex quicklatex : t

  show : t → String
  show (ghc main?) = if main? then "GHC" else "GHC (no main)" 
  show js = "JS"
  show html = "HTML"
  show latex = "LaTeX"
  show quicklatex = "QuickLaTeX"

  encode : t → String
  encode (ghc main?) = if main? then "GHC" else "GHCNoMain"
  encode t = show t
open Backend using (ghc ; js ; html ; latex ; quicklatex) public

module ComputeMode where
  data t : Set where
    default head ignore-abstract use-show-instance : t

  decoder : Decoder t
  decoder = string >>= λ where
    "DefaultCompute" → succeed default
    "HeadCompute" → succeed head
    "IgnoreAbstract" → succeed ignore-abstract
    "UseShowInstance" → succeed use-show-instance
    _ → ⊘

  -- TODO: make case doesn't work here
  show : t → String
  show default = "DefaultCompute"
  show head = "HeadCompute"
  show ignore-abstract = "IgnoreAbstract"
  show use-show-instance = "UseShowInstance"

module AgdaCommand where
  data t : Set where
    load : t
    -- Give uses the boolean to indicate whether force should be used,
    -- refine uses the boolean to indicate whether the compiler should create case lambdas when intro'ing functions
    give refine-or-intro : Bool → InteractionPoint.t → t
    context goal-type infer goal-type-context goal-type-context-infer
      goal-type-context-check auto-goal module-contents-goal
        : Rewrite.t → InteractionPoint.t → t
    show-metas show-constraints : Rewrite.t → t
    make-case why-in-scope-goal : InteractionPoint.t → t
    compile-file : Backend.t → t
    compute-goal : ComputeMode.t → InteractionPoint.t → t
    compute-toplevel : ComputeMode.t → String → t
    module-contents-toplevel search-about-toplevel infer-toplevel : Rewrite.t → String → t
    why-in-scope-toplevel : String → t
    toggle-hidden toggle-irrelevant : t

  show-pos : Nat → TextDocument.t → String
  show-pos offset doc =
    let pos = TextDocument.position-at doc offset
     in "Pn () " ++ Nat.show (offset + 1) ++ " " ++ Nat.show (Position.line pos) ++ " " ++ Nat.show (Position.char pos)

  show-range : TextDocument.t → OffsetRange.t → String
  show-range doc (offset-range start length) =
    let range = "Interval () (" ++ show-pos start doc ++ ") (" ++ show-pos (start + length) doc ++ ")"
     in "intervalsToRange Nothing [" ++ range ++ "]"

  show-goal-command : TextDocument.t → InteractionPoint.t → List String
  show-goal-command doc ip = 
    let goal-content = doc |> TextDocument.get-text (OffsetRange.to-vsc-range doc $ InteractionPoint.content-range ip) in
    let sanitised-goal-content = replace "\"" "\\\"" goal-content in

    -- We send along an up-to-date version of the interaction point's range.
    -- Agda uses this range to update its internal state, and return a correct range when responding with a
    -- give action. This means we can fully rely on the new locations provided by Agda without consulting
    -- our own cache again.
    --
    -- NOTE: There does seem to be a bug/inconsistency in the compiler when sending the range including markers,
    -- where the locations of errors within the hole are reported incorrectly in the display infos.
    show-Nat (ip .id) ∷ ("(" ++ show-range doc (ip .range) ++ ")") ∷ ("\"" ++ sanitised-goal-content ++ "\"") ∷ []

  show-goal-rewrite-command : TextDocument.t → Rewrite.t → InteractionPoint.t → List String
  show-goal-rewrite-command doc r ip = Rewrite.show r ∷ show-goal-command doc ip

  show-list : TextDocument.t → t → List String
  show-list doc load = "Cmd_load" ∷ "\"" ++ TextDocument.file-name doc ++ "\"" ∷ "[]" ∷ []
  show-list doc (give with-force ip) =
    let force = if with-force then "WithForce" else "WithoutForce" in
    "Cmd_give" ∷ force ∷ show-goal-command doc ip
  show-list doc (refine-or-intro b ip) =
    let b' = if b then "True" else "False" in
    "Cmd_refine_or_intro" ∷ b' ∷ show-goal-command doc ip
  show-list doc (context r ip) = "Cmd_context" ∷ show-goal-rewrite-command doc r ip
  show-list doc (goal-type r ip) = "Cmd_goal_type" ∷ show-goal-rewrite-command doc r ip
  show-list doc (infer r ip) = "Cmd_infer" ∷ show-goal-rewrite-command doc r ip
  show-list doc (goal-type-context r ip) = "Cmd_goal_type_context" ∷ show-goal-rewrite-command doc r ip
  show-list doc (goal-type-context-infer r ip) = "Cmd_goal_type_context_infer" ∷ show-goal-rewrite-command doc r ip
  show-list doc (goal-type-context-check r ip) = "Cmd_goal_type_context_check" ∷ show-goal-rewrite-command doc r ip
  show-list doc (auto-goal r ip) = "Cmd_autoOne" ∷ show-goal-rewrite-command doc r ip
  show-list doc (module-contents-goal r ip) = "Cmd_show_module_contents" ∷ show-goal-rewrite-command doc r ip
  show-list doc (show-constraints r) = "Cmd_constraints" ∷ Rewrite.show r ∷ []
  show-list doc (show-metas r) = "Cmd_metas" ∷ Rewrite.show r ∷ []
  show-list doc (make-case ip) = "Cmd_make_case" ∷ show-goal-command doc ip
  show-list doc (why-in-scope-goal ip) = "Cmd_why_in_scope" ∷ show-goal-command doc ip
  show-list doc (compile-file backend) = "Cmd_compile" ∷ Backend.encode backend ∷ ("\"" ++ TextDocument.file-name doc ++ "\"") ∷ "[]" ∷ []
  show-list doc (compute-goal mode ip) = "Cmd_compute" ∷ ComputeMode.show mode ∷ show-goal-command doc ip
  show-list doc (compute-toplevel mode term) = "Cmd_compute_toplevel" ∷ ComputeMode.show mode ∷ ("\"" ++ term ++ "\"") ∷ []
  show-list doc (module-contents-toplevel r name) = "Cmd_show_module_contents_toplevel" ∷ Rewrite.show r ∷ ("\"" ++ name ++ "\"") ∷ []
  show-list doc (infer-toplevel r term) = "Cmd_infer_toplevel" ∷ Rewrite.show r ∷ ("\"" ++ term ++ "\"") ∷ []
  show-list doc (search-about-toplevel r query) = "Cmd_search_about_toplevel" ∷ Rewrite.show r ∷ ("\"" ++ query ++ "\"") ∷ []
  show-list doc (why-in-scope-toplevel term) = "Cmd_why_in_scope_toplevel" ∷ ("\"" ++ term ++ "\"") ∷ []
  show-list doc toggle-hidden = "ToggleImplicitArgs" ∷ []
  show-list doc toggle-irrelevant = "ToggleIrrelevantArgs" ∷ []

  show : TextDocument.t → t → String
  show = intercalate " " ∘₂ show-list

module AgdaInteraction where
  record t : Set where
    constructor iotcm
    field
      file : TextDocument.t
      command : AgdaCommand.t
  open t public

  show : t → String
  show (iotcm file cmd) =
    let path = TextDocument.file-name file in
    "IOTCM \"" ++ path ++ "\" NonInteractive Direct (" ++ AgdaCommand.show file cmd ++ ")\n"

  from-AgdaCommand : AgdaCommand.t → IO (Maybe t)
  from-AgdaCommand cmd = do
    just e ← TextEditor.active-editor where _ → pure nothing
    doc ← TextEditor.document e
    pure $ just (iotcm doc cmd)

  under-cursor-command : Model → (InteractionPoint.t → AgdaCommand.t) → IO (Maybe t)
  under-cursor-command model cmd = do
    just e ← TextEditor.active-editor where _ → pure nothing
    doc ← TextEditor.document e
    cursor ← TextDocument.offset-at doc <$> TextEditor.cursor-pos e
    model .loaded-files !? TextDocument.file-name doc |> maybe (pure nothing) λ (record { interaction-points = ips }) → do
     ips |> find (λ ip → OffsetRange.contains? (ip .range) cursor) |> maybe (pure nothing) λ ip → do
      pure $ just (iotcm doc (cmd ip))

  input-prompt-command : (String → AgdaCommand.t) → IO (Maybe t)
  input-prompt-command cmd = do
    just e ← TextEditor.active-editor where _ → pure nothing
    doc ← TextEditor.document e
    input ← from-Maybe "" <$> Window.show-input-box
    pure $ just (iotcm doc (cmd input))
    
open AgdaInteraction using (iotcm ; file ; command) public

module Config where
  record t : Set where
    constructor mkConfig
    field
      agda-path : Maybe String
      extra-args : String
  open t public

  decoder : Decoder t
  decoder = do
    path ← required "agda-path" string <&> λ s → if s String.== "" then nothing else just s
    mkConfig path <$> required "extra-args" string

  postulate invalid : {A : Set} → IO A
  {-# COMPILE JS invalid = A => async () => { throw "call to invalid" } #-}

  load : IO t
  load = do
    just config ← decoder <$> Workspace.get-configuration "agda-mode"
      where _ → invalid -- TODO: Show an error to the user
    pure config
open Config using (agda-path ; extra-args) public
